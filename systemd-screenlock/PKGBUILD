# Maintainer: Till Smejkal <till.smejkal+packages@ossmail.de>

pkgname=systemd-screenlock
pkgver=1.1
pkgrel=1
pkgdesc="Session locking using systemd"
url="https://github.com/l3nkz/systemd-screenlock"
depends=('systemd' 'python' 'python-dbus' 'python-gobject')
license=('GPL3')
arch=('any')

source=("https://github.com/l3nkz/systemd-screenlock/archive/v${pkgver}.tar.gz")
sha512sums=('28b3f58c36e58fc913501634672058bd04b4904b5f1a8c866e2ff422c219ff0b484bee4db9fdfc9d1f2687598617faad6121702406f2114a45d1a81a9d7e3707')

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Install the binary
    install -D -m 755 lock-forward "${pkgdir}"/usr/bin/lock-forward

    # Install the service files
    install -D -m 644 screenlock.service "${pkgdir}"/usr/lib/systemd/system/screenlock.service
    install -D -m 644 lock-forward@.service "${pkgdir}"/usr/lib/systemd/user/lock-forward@.service
}
