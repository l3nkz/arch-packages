# Maintainer: Till Smejkal <till.smejkal+packages@ossmail.de>

# Own variables
_pkgbase=pynoter
_pkgsubver="-alpha2"

# makepkg variables
pkgname=python-pynoter
pkgver=1.0
pkgrel=2
pkgdesc="Python library for advanced desktop notifications."
url="https://github.com/l3nkz/pynoter"
depends=('python' 'python-dbus' 'python-gobject')
makedepends=('python-setuptools')
license=('GPL3')
arch=('any')
source=("https://github.com/l3nkz/pynoter/archive/v${pkgver}${_pkgsubver}.tar.gz")
sha512sums=(f4c431b5cc7f9f50def977bc2c96e97d217be3efab0c97c24b42c6c7adc8bd68f12b1d38322709bdcdf331a88bd463a030ca4c79984e03207df35306544c98a5)


package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}${_pkgsubver}"

    # Install the python package.
    python setup.py install --root="${pkgdir}/" --optimize=1

    # Install the pynoter server binary and systemd service file.
    install -D -m 755 pyNoter "${pkgdir}/usr/bin/pyNoter"
    install -D -m 644 pynoter.service "${pkgdir}/usr/lib/systemd/user/pynoter.service"
}
