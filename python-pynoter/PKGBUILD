# Maintainer: Till Smejkal <till.smejkal+packages@ossmail.de>

# Own variables
_pkgbase=pynoter
_pkgsubver="-beta2"

# makepkg variables
pkgname=python-pynoter
pkgver=1.0
pkgrel=6
pkgdesc="Python library for advanced desktop notifications."
url="https://github.com/l3nkz/pynoter"
depends=('python' 'python-dbus' 'python-gobject')
makedepends=('python-setuptools')
license=('GPL3')
arch=('any')
source=("https://github.com/l3nkz/pynoter/archive/v${pkgver}${_pkgsubver}.tar.gz")
sha512sums=('511f1e2cb8ea4ad70dc2d54a41b23fce9900cfc4ff93e63108ef39b7d55d855d31cbbf13b437be78fbf1e21228f0256921533add939359d3bbf614c4d333163c')


package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}${_pkgsubver}"

    # Install the python package.
    python setup.py install --root="${pkgdir}/" --optimize=1

    # Install the pynoter server binary and systemd service file.
    install -D -m 755 pyNoter "${pkgdir}/usr/bin/pyNoter"
    install -D -m 644 contrib/pynoter.service "${pkgdir}/usr/lib/systemd/user/pynoter.service"
}
