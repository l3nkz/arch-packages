# Maintainer: Till Smejkal <till.smejkal+packages@ossmail.de>

# makepkg variables
pkgname=python-pynoter
pkgver=1.0
pkgrel=1
pkgdesc="Python library for advanced desktop notifications."
url="https://github.com/l3nkz/pynoter"
depends=('python' 'python-dbus' 'python-gobject')
makedepends=('python-setuptools')
license=('GPL3')
arch=('any')
source=("https://github.com/l3nkz/pynoter/archive/v${pkgver}-alpha.tar.gz")
sha512sums=(e1e039ee25be8b94b73304edb316549a3ff647ef0de1ebd20eb3ee7e8097d7bfbbb413a910582f4e57ac2bc7079bcf724e99b932804f806bdb3b6f3a0e2a0cb5)

# Own variables
_pkgbase=pynoter

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}-alpha"

    # Install the python package.
    python setup.py install --root="${pkgdir}/" --optimize=1

    # Install the pynoter server binary and systemd service file.
    install -D -m 755 pyNoter "${pkgdir}/usr/bin/pyNoter"
    install -D -m 644 pynoter.service "${pkgdir}/usr/lib/systemd/user/pynoter.service"
}
