# Contributor: Till Smejkal <till.smejkal(at)ossmial.de>
# Contributor: Cheer Xiao <xiaqqaix AT gmail DOT com>
# Contributor: Leo Scherer <aur AT leo-scherer DOT de>
# original Contributor: Niklas Schmuecker (IRC: nisc) <nschmuecker (gmail)>

pkgname=brightd
pkgver=0.4.1
pkgrel=4
pkgdesc='Automatically dims screen when there is no user input.'
arch=('i686' 'x86_64')
license=('GPL2')
url='http://www.pberndt.com/Programme/Linux/brightd/'
depends=('libxss')
install='brightd.install'

source=("http://www.pberndt.com/raw/Programme/Linux/brightd/_download/brightd-${pkgver}.tar.bz2" 
        "brightd.service"
        "proposal.patch")

sha256sums=('ee1ab935bafbc9f21d32e771c2b99c107f059a87094441367089ad38e813b78e'
            'db16fcbac8a5a9797ff743178a134684767fb93cf6f71d1eff490b384aa20a21'
            'f491fb7aeecf8fe86742a950fcbb21365e6609b295ecf22fceee502159c1284b')

build() {
  cd "${srcdir}/brightd-${pkgver}"

  # Fixes AC-detection by moving to /sys, blocks intel_backlight
  # and makes fade smoother
  patch -p1 -i "${srcdir}"/proposal.patch
  make
}

package() {
  cd "${srcdir}/brightd-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dsm755 "${srcdir}/brightd-${pkgver}/brightd" \
                  "${pkgdir}/usr/bin/brightd"
  install -Dm644 "${srcdir}/brightd-${pkgver}/brightd.1" \
                 "${pkgdir}/usr/share/man/man1/brightd.1"
  install -Dm644 "${srcdir}/brightd-${pkgver}/README" \
                 "${pkgdir}/usr/share/doc/brightd/README"
  install -Dm644 "${srcdir}/brightd.service" \
                 "${pkgdir}/usr/lib/systemd/system/brightd.service"
}
