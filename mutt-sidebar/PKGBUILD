# Maintainer: Till Smejkal <till.smejkal+packages@ossmail.de>
# Contributor: Alessio Sergi <asergi at archlinux dot us>
# Contributor: tobias [tobias [at] archlinux.org]
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname=mutt-sidebar
pkgver=1.6.0
pkgrel=1
pkgdesc='Small but very powerful text-based mail client'
arch=('i686' 'x86_64')
url='http://www.mutt.org/'
license=('GPL')
depends=('gpgme' 'krb5' 'libidn' 'mime-types')
optdepends=('smtp-forwarder: to send mail')
conflicts=('mutt')
provides=('mutt')
source=("ftp://ftp.mutt.org/pub/mutt/mutt-${pkgver}.tar.gz"
        "sidebar.patch"
        "attachments.patch"
        "trashfolder.patch")
sha512sums=('601d5f70c7cd30903799714cd85b80f9650a029e621d044075e123656411dde809d5cef24a40ba49860bc242cf4a0b914c703deb5a7125b3a24eeb93f4ae3c4c'
            '26d594a053e7f849b09d161335642336f0dfe033fb506dd49dac65026a4f48e65eeff35db29149d2e336b2b11d7a136fe9271af7e9427ed7b6bef3bbd7cd6ba6'
            '24d943d512fa5ceaf76ab7342642255769cb70acb08d061d57d0a2cd15e88b5719af501501585a9a67ada37bee7e8dd592847b416e24e764084b39ca132b798b'
            '8d607738f4683890e2ef80f4cc104f0ed7be8e519f3cc4c944d1c7415518fd8d365c62d0fb0799e8cb7db19ca2459a7d94858877b87ed6fb1c12d6d6e19ea193')

prepare() {
  cd "$srcdir/mutt-$pkgver"

  # apply patches
  patch -Np1 -i "$srcdir"/sidebar.patch
  patch -Np1 -i "$srcdir"/attachments.patch
  patch -Np1 -i "$srcdir"/trashfolder.patch

  # fix automake issue
  autoreconf -vfi
}

build() {
  cd "$srcdir/mutt-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --enable-gpgme --enable-pop \
              --enable-imap --enable-smtp \
              --enable-hcache --with-curses=/usr \
              --with-regex --with-gss=/usr \
              --with-ssl=/usr --with-sasl \
              --with-idn
  make
}

package() {
  cd "$srcdir/mutt-$pkgver"
  make DESTDIR="$pkgdir" install

  # remove unneeded or conflicting files
  rm "$pkgdir"/etc/mime.types{,.dist}
  rm "$pkgdir"/usr/bin/{flea,muttbug}
  rm "$pkgdir"/usr/share/man/man1/{flea,muttbug}.1

  # install Muttrc.gpg file
  install -D -m 644 contrib/gpg.rc "$pkgdir"/etc/Muttrc.gpg.dist
}
